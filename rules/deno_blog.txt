# github.com/dcdunkan/deno-bot

~version: "2.1"
~allowed_origin: "https://deno.com"

?path: /blog/.* 

# svg2png is awesome!
@replace("^/", "https://svg2png.deno.dev/https://deno.com/"): //img[ends-with(@src, ".svg")]/@src

site_name: "Deno Blog"
channel: "@deno_news"

@if ( //body/div/div/div[1]/img ) {
  cover: //img[1]
}

title: //h1
image_url: //head/meta[@property="og:image"]/@content
kicker: //head/meta[@name="description"]/@content
author: //article/div/p[2]
published_date: //time
body: //article

# FIXES
# Move <img> from <p> to outside ...
@split_parent: //a/img
@split_parent: //p/img
@split_parent: //p/video
# Remove author links
@remove: //article/div[1]/p[2]
# Remove date
@remove: //article/div[1]/p[1]
# Remove the weird ruler (I hate its preview)
@remove: //hr

# ANCHOR FIX (Thanks to @KnorpelSenf)
# Rewrite links to stay within IV ...
# @replace("^/", "https://t.me/iv?rhash=28aee3eda1037a&url=https%3A%2F%2Fdeno.com%2Fblog%2F"): //a/@href
@replace("https://deno.com/blog/", "https://t.me/iv?rhash=28aee3eda1037a&url=https%3A%2F%2Fdeno.com%2Fblog%2F"): //a/@href
# Anchor stuff
@set_attr(name, @href): //a[has-class("anchor")]
# ... and stripping leading hashes for anchor names ...
@replace("^#", ""): $$/@name
